apply plugin: 'java'
apply plugin: 'application'

// Java Version Compatibility
sourceCompatibility = 1.8
targetCompatibility = 1.8

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

version = '1.0'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'org.projectlombok', name: 'lombok', version: '1.14.8'
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: '0.7.1'
    compile group: 'io.dropwizard', name: 'dropwizard-assets', version: '0.7.1'
    compile group: 'io.dropwizard', name: 'dropwizard-views', version: '0.7.1'
    compile group: 'io.dropwizard', name: 'dropwizard-views-freemarker', version: '0.7.1'
    compile group: 'io.dropwizard', name: 'dropwizard-jdbi', version: '0.7.1'
    compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '1.1.7'
    compile group: 'com.wordnik', name: 'swagger-jersey-jaxrs_2.10', version: '1.3.2'
    compile group: 'com.google.inject.extensions', name: 'guice-multibindings', version: '3.0'
    compile group: 'com.google.inject.extensions', name: 'guice-servlet', version: '3.0'
    compile group: 'com.sun.jersey.contribs', name: 'jersey-guice', version: '1.17.1'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.+'
}

// main class of the application
mainClassName = 'com.fooorg.fooproj.app.SampleService'
applicationDefaultJvmArgs = []

run {
    args 'server', './config/sample.yml'
}

test {
    useJUnit {
        includeCategories 'com.fooorg.fooproj.framework.FooProjUnitTest'
    }
}

task integrationTest(type: Test) {
    useJUnit {
        includeCategories 'com.fooorg.fooproj.framework.FooProjIntegrationTest'
    }
}

tasks.withType(Test) {
    testLogging {
        events 'passed'
    }
}
